buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

import groovy.json.*

configurations {
    compile
    deployJars
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "maven"

group = "net.doubledoordev.fsw"
version = "2.2.1"

targetCompatibility = 1.8
sourceCompatibility = 1.8

archivesBaseName = 'ForgeSubWhitelist'
def githuborg = 'DoubleDoorDevelopment'
def description = 'Automatic sub login checker for Twitch, GameWisp, Beam, ...'
minecraft {
    version = "1.12.2-14.23.0.2491"
    runDir = "run"
    mappings = "snapshot_20171017"
}

if (System.getenv().BUILD_NUMBER != null) version += "." + System.getenv().BUILD_NUMBER

//noinspection GroovyAssignabilityCheck
processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    doLast {
        def updateFile = new File(project.archivesBaseName + '.json')
        def json;
        if (updateFile.exists()) {
            json = new JsonSlurper().parseText(updateFile.getText())
        }
        else {
            def builder = new JsonBuilder()
            json = builder(
                    homepage: "http://doubledoordev.net/",
                    promos: new HashMap<>()
            )
        }
        def outStream = new ByteArrayOutputStream()
        def result = exec {
            executable = 'git'
            args = [ 'log', '-n', '1', "--format='%B'"]
            standardOutput = outStream
        }
        def fullLog = outStream.toString().replaceAll("^\\s*'\\s*|\\s*'\\s*\$", "").replaceAll("[\\r\\n]+", "\n")

        json['promos'][project.minecraft.version + '-latest'] = project.version
        json['promos'][project.minecraft.version + '-recomended'] = project.version
        if (!json.containsKey(project.minecraft.version)) json.put(project.minecraft.version, new HashMap<>())
        def version = json[project.minecraft.version]
        version.put(project.version, fullLog)
        updateFile.write JsonOutput.prettyPrint(JsonOutput.toJson(json))
    }
}

task deobfJar(type: Jar, dependsOn: 'jar') {
    from sourceSets.main.output
    from "LICENSE.txt"
    classifier "dev"
    appendix = project.minecraft.version
    manifest {
        attributes 'ModSide': 'SERVER'
    }
}
sourceJar {
    from "LICENSE.txt"
    exclude("com/**")
    classifier "src"
    appendix = project.minecraft.version
    manifest {
        attributes 'ModSide': 'SERVER'
    }
}
//noinspection GroovyAssignabilityCheck
jar {
    from "LICENSE.txt"
    exclude("com/**")
    appendix = project.minecraft.version
    manifest {
        attributes 'ModSide': 'SERVER'
    }
}
artifacts {
    archives deobfJar
}
